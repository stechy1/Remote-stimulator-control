apply plugin: 'com.android.application'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    defaultConfig {
        applicationId 'cz.zcu.fav.remotestimulatorcontrol'
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            testCoverageEnabled true
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    dexOptions {
        // Skip pre-dexing when running on Travis CI or when disabled via -Dpre-dex=false.
        preDexLibraries = preDexEnabled && !travisBuild
    }
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:24.2.0'
    }
    lintOptions {
        abortOnError = !travisBuild
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile(
            [group: 'com.android.support', name: 'appcompat-v7', version: supportLibVersion],
            [group: 'com.android.support', name: 'design', version: supportLibVersion],
            [group: 'com.android.support', name: 'support-vector-drawable', version: supportLibVersion],
            [group: 'com.android.support', name: 'recyclerview-v7', version: supportLibVersion],
            [group: 'com.android.support', name: 'cardview-v7', version: supportLibVersion],
            [group: 'com.android.support', name: 'palette-v7', version: supportLibVersion]
    )
    compile 'com.google.android:flexbox:0.2.2'
    compile 'jp.wasabeef:recyclerview-animators:2.2.3'
    testCompile([group: 'org.mockito', name: 'mockito-all', version: '1.10.19'])
    testCompile 'junit:junit:4.12'
    androidTestCompile(
            [group: 'com.android.support', name: 'support-annotations', version: supportLibVersion]
    )
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}